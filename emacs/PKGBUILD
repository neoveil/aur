# my own build of emacs with some different options
# stolen from official arch emacs build @ https://gitlab.archlinux.org/archlinux/packaging/packages/emacs

pkgname=emacs
pkgver=30.1
pkgrel=3
pkgdesc='GNU Emacs'
arch=('x86_64')
url='https://www.gnu.org/software/emacs/'
license=('GPL3')
depends=(
  gmp
  gnutls
  lcms2
  libacl.so
  libasound.so
  libgccjit
  libdbus-1.so
  libfontconfig.so
  libfreetype.so
  libgdk-3.so
  libgdk_pixbuf-2.0.so
  libgif.so
  libgio-2.0.so
  libglib-2.0.so
  libgobject-2.0.so
  libgpm.so
  libgtk-3.so
  libharfbuzz.so
  libice
  libjpeg.so
  libncursesw.so
  libotf
  libpango-1.0.so
  libpng
  librsvg-2.so
  libsm
  sqlite libsqlite3.so
  libsystemd.so
  libtiff.so
  libtree-sitter.so
  libwebp.so
  libwebpdemux.so
  libxfixes
  libxml2.so
  m17n-lib
  zlib
  webkit2gtk
  mailutils
  imagemagick
)
makedepends=(libgccjit)
source=(https://ftp.gnu.org/gnu/emacs/${pkgname}-${pkgver}.tar.xz)
sha256sums=('6ccac1ae76e6af93c6de1df175e8eb406767c23da3dd2a16aa67e3124a6f138f')
options=(!strip)

build() {
  export ac_cv_lib_gif_EGifPutExtensionLast=yes
  cd ${pkgname}-${pkgver}

  ./autogen.sh
  ./configure \
    --sysconfdir=/etc \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-modules \
    --with-tree-sitter \
    --with-xwidgets \
    --with-cairo \
    --with-harfbuzz \
    --with-libsystemd \
    --without-compress-install \
    --with-mailutils \
    --with-imagemagick \
    --disable-build-details \
    --with-x-toolkit=gtk3 \
    --with-native-compilation=aot

  make -j$(nproc)
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  chown -R root:root "${pkgdir}/usr/share/emacs/${pkgver}"
}
